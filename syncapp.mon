
using com.apama.cumulocity.ManagedObject;
using com.apama.cumulocity.Util;
using com.apama.util.AnyExtractor;
using com.apama.json.JSONPlugin;
using com.apama.functional.Fn;
using com.apama.functional.Partial;
using com.apama.functional.Functional;
using com.apama.cumulocity.FindTenantOptions;
using com.apama.cumulocity.FindTenantOptionsResponse;
using com.apama.cumulocity.FindManagedObject;
using com.apama.cumulocity.FindManagedObjectResponse;
using com.apama.cumulocity.FindManagedObjectResponseAck;
using com.apama.cumulocity.TenantOption;
using com.softwareag.connectivity.httpclient.HttpTransport;
using com.softwareag.connectivity.httpclient.HttpOptions;
using com.softwareag.connectivity.httpclient.Request;
using com.softwareag.connectivity.httpclient.Response;

event Base64
{
	constant string b64chars := "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/";
	constant string a7chars := " !\"#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\\]^_`abcdefghijklmnopqrstuvwxyz{|}~ ";
	static action decode4(string base64, integer start, integer end) returns string
	{
		integer b1 := 0;
		if start <= end { b1 := b64chars.find(base64.substring(start, start+1)); }
		integer b2 := 0;
		if start+1 <= end { b2 := b64chars.find(base64.substring(start+1, start+2)); }
		integer b3 := 0;
		if start+2 <= end { b3 := b64chars.find(base64.substring(start+2, start+3)); }
		integer b4 := 0;
		if start+3 <= end { b4 := b64chars.find(base64.substring(start+3, start+4)); }

		integer c1 := (b1<<2)+(b2>>4);
		integer c2 := ((b2 and 15)<<4) + (b3>>2);
		integer c3 := b4+((b3 and 3)<<6);

		string rv := "";
		if c1 = 10 { rv := rv+"\n"; }
		else if c1 >=32 and c1 <= 127  { rv := rv+a7chars.substring(c1-32,c1-31); }
		if c2 = 10 { rv := rv+"\n"; }
		else if c2 >=32 and c2 <= 127  { rv := rv+a7chars.substring(c2-32,c2-31); }
		if c3 = 10 { rv := rv+"\n"; }
		else if c3 >=32 and c3 <= 127 { rv := rv+a7chars.substring(c3-32,c3-31); }
		return rv;
	}
	static action decode(string base64) returns string
	{
		base64 := base64.replaceAll("\n", "");
		string s := "";
		integer i := 0;
		integer l := base64.length();
		while i < l {
			integer remaining := i+4;
			if remaining >= l { remaining := l-1; }
			s := s + decode4(base64, i, remaining);
			i := i + 4;
		}
		return s;
	}
}

event RequestBuilder
{
	string repo;
	string auth;
	HttpTransport http;
	string branch;
	action createRestAPIQuery(string method, string path, any content) returns Request
	{
		log "createRequestAPIQuery("+method+", "+path.replaceAll(repo, "")+", "+content.toString()+")";
		Request req := http.createRequest(method, path.replaceAll(repo, ""), content, <HttpOptions>Fn.setFields(new HttpOptions, {"headers":{"Accept":"application/vnd.github+json", "Authorization":auth}}));
		req.id := integer.incrementCounter("com.softwareag.connectivity.httpclient.HTTPClientGeneric"); 
		return req;
	}
}

event GITFile
{
	static action create(string path, any tree, RequestBuilder builder) returns GITFile
	{
		return <GITFile>Fn.setFields(new GITFile, [path, tree, builder]);
	}
	string path;
	any tree;
	RequestBuilder builder;
	any nextAction;
	string content;
	action getFromGit(any nextAction)
	{
		sequence<com.softwareag.connectivity.httpclient.Request> dummy;
		self.nextAction := nextAction;
		log "getFromGit(tree="+tree.toString()+")";
		any _ := Functional(tree)
			.filter(Fn.partial(Fn.fieldEqual, ["path", path]))
			.map(Fn.getEntry("url"))
			.map(Fn.partial(builder.createRestAPIQuery, ["GET", Fn.$(1), new any]))
			.map(Fn.callAction("execute", readContent));
	}
	action readContent(Response r)
	{
		content := Base64.decode(r.payload.getString("content"));
		any _ := nextAction.getGenericAction()(new sequence<any>);
	}
}

event GetObject
{
	RequestBuilder builder;
	any next;
	action urlImpl(string url, any next, any arg)
	{
		self.next := next;
		url := url.replaceAll("$1", <string>arg);
		builder.createRestAPIQuery("GET", url, new any).execute(invokeNextWithBody);
	}
	action invokeNextWithBody(Response r)
	{
		any _ := next.getGenericAction()([<any>r.payload]);
	}
	action url(string url, any next) returns Partial { return Fn.partial(urlImpl, [url, next]); }
	action extractImpl(string path, any next, AnyExtractor object)
	{
		any _ := next.getGenericAction()([object.getAny(path)]);
	}
	action extract(string path, any next) returns Partial { return Fn.partial(extractImpl, [path, next]); }
	action setImpl(any object, string field, any next, any value)
	{
		object.setField(field, value);
		any _ := next.getGenericAction()([object]);
	}
	action set(any object, string field, any next) returns Partial { return Fn.partial(setImpl, [object, field, next]); }
}
event EPLFile
{
	string c8yname;
	string c8ydescription;
	string c8ystate;
	string c8ycontent;
	string c8yid;
	string gitdescription;
	string gitstate;
	string gitcontent;
	string gitpath;
	any gittree;
	any update;
	RequestBuilder builder;

	action needsUpdate() returns boolean
	{
		return
			c8ydescription != gitdescription or
			c8ystate != gitstate or
			c8ycontent != gitcontent;
	}
	action checkForUpdate(action<> onUpdateNeeded)
	{
		if needsUpdate() { onUpdateNeeded(); }
	}

	static action createFromC8Y(any update, string id, string name, string description, string state, string content) returns EPLFile
	{
		return <EPLFile> Fn.setFields(new EPLFile,
				{"c8yid":id, "c8yname":name, "c8ydescription":description, "c8ystate":state, "c8ycontent":content,
				"update":update, "builder":update.getEntry("builder")});
	}
	
	static action createFromGit(any update, string gitpath, any tree) returns EPLFile
	{
		return <EPLFile> Fn.setFields(new EPLFile,
				{"gitpath":gitpath, "gittree":tree,
				"update":update, "builder":update.getEntry("builder")});
	}

	action commitToGit()
	{
		any _ := Functional({
			"epl/"+c8yname+".mon":c8ycontent,
			"epl/"+c8yname+".state": c8ystate,
			"epl/"+c8yname+".txt": c8ydescription
		})
			.map(update.getAction("writeFile"))
			.map(update.getAction("getWriteResponse"));
			//.waitForAllCompleted("com.softwareag.connectivity.httpclient.Response", "id", update.doCommit);
	}

	action uploadToC8Y()
	{
		send Fn.setFields(new ManagedObject, {"id":<any>c8yid, "type":"apama_eplfile", "name":c8yname, "params":{"apama_eplfile.contents":c8ycontent, "apama_eplfile.description":c8ydescription, "apama_eplfile.state":c8ystate}}) to ManagedObject.SEND_CHANNEL;
	}

	action updateFromGit(any completedAction)
	{
		if gittree.empty() {
			GetObject go := GetObject(builder, new any);
			go.url("/git/ref/heads/"+builder.branch,
				go.extract("object.url",
					go.url("$1",
						go.extract("tree.url",
							go.url("$1?recursive=1",
								go.extract("tree",
									go.set(self, "gittree",
										Fn.partial(updateFromGitImpl, completedAction).exec
										)))))));
		} else {
			updateFromGitImpl(completedAction);
		}
	}
	action updateFromGitImpl(any completedAction)
	{
		GITFile monfile := GITFile.create(gitpath, gittree, builder);
		GITFile statefile := GITFile.create(gitpath.replaceAll(".mon", ".state"), gittree, builder);
		GITFile txtfile := GITFile.create(gitpath.replaceAll(".mon", ".txt"), gittree, builder);
		monfile.getFromGit(Fn.partial(statefile.getFromGit, Fn.partial(txtfile.getFromGit, Fn.partial(readGitResponses, [monfile, statefile, txtfile, completedAction]).exec).exec).exec);
	}

	action readGitResponses(GITFile monfile, GITFile statefile, GITFile txtfile, any completedAction)
	{
		gitcontent := monfile.content;
		gitstate := statefile.content;
		gitdescription := txtfile.content;
		any _ := completedAction.getGenericAction()(new sequence<any>);
	}
	
	action updateFromC8Y(any completedAction)
	{
		integer reqId := Util.generateReqId();
		on FindManagedObjectResponse(reqId=reqId) as resp and not FindManagedObjectResponseAck(reqId=reqId)
		{
			AnyExtractor ae := AnyExtractor(resp.managedObject.params);
			c8ycontent := ae.getStringOr("apama_eplfile.contents", "");
			c8ydescription := ae.getStringOr("apama_eplfile.description", "");
			c8ystate := ae.getStringOr("apama_eplfile.state", "");
			c8yid := resp.managedObject.id;
			c8yname := resp.managedObject.name;
		}
		on FindManagedObjectResponseAck(reqId=reqId)
		{
			any _ := completedAction.getGenericAction()(new sequence<any>);
		}
		send FindManagedObject(reqId, "", {"type":"apama_eplfile", "name": gitpath.replace("epl/(.*).mon", "$1")}) to FindManagedObject.SEND_CHANNEL;
	}
}
event GitUpdate
{
	static action create(string repo, string auth, string branch, HttpTransport http, sequence<optional<string>> latestHash) returns GitUpdate
	{
		return <GitUpdate> Fn.setFields(new GitUpdate, [<any> RequestBuilder(repo, auth, http, branch), branch, latestHash]);
	}

	RequestBuilder builder;
	string branch;
	sequence<optional<string>> latestHash;
	dictionary<string,string> commit;
	string headcommithash;

	action getWriteResponse(string name, integer requestId) returns integer
	{
		on Response(id=requestId) as r {
			log "Got write response "+r.toString();
			commit[name] := r.payload.getString("sha");
			if commit.size() = 3 { doCommit(); }
		}
		return requestId;
	}
	action writeFile(string name, string data) returns integer
	{
		log "Writing file "+name+" ("+data+")";
		Request r := builder.createRestAPIQuery("POST", "/git/blobs", {"content":data, "encoding":"utf-8"});
		send r to r.channel;
		return r.id;
	}
	action pathNotInCommits(any treeentry) returns boolean
	{
		AnyExtractor ae := AnyExtractor(treeentry);
		return not commit.hasKey(ae.getStringOr("path", ""));
	}
	static action createTreeEntry(string path, string hash) returns any
	{
		return { 
			"path": path,
			"mode": "100644",
			"type": "blob",
			"sha": hash
			};
	}
	action doCommit()
	{
		log "Committing to "+builder.repo+": "+commit.toString();
		builder.createRestAPIQuery("GET", "/git/ref/heads/"+branch, new any).execute(getHeadRef);
	}
	action getHeadRef(Response r)
	{
		log "Got response: "+r.toString();
		string headrefhash := r.payload.getString("object.sha");
		string headrefurl := r.payload.getString("object.url");
		builder.createRestAPIQuery("GET", headrefurl, new any).execute(getHeadCommit);
	}
	action getHeadCommit(Response r)
	{
		log "Got response: "+r.toString();
		headcommithash := r.payload.getString("sha");
		string headtreehash := r.payload.getString("tree.sha");
		string headtreeurl := r.payload.getString("tree.url");
		builder.createRestAPIQuery("GET", headtreeurl+"?recursive=1", new any).execute(createTree);
	}
	action createTree(Response r)
	{
		log "Got response: "+r.toString();
		sequence<any> treedata := <sequence<any>> Functional(r.payload.getAny("tree")).filter(Fn.isNot(Fn.partial(Fn.fieldEqual, ["type", "tree"]))).filter(pathNotInCommits).get();
		any _ := Functional(commit).filter(Fn.isNot(Fn.isEmpty)).map(createTreeEntry).map(treedata.append);
		builder.createRestAPIQuery("POST", "/git/trees", {"tree":treedata}).execute(createCommit);
	}
	action createCommit(Response r)
	{
		log "Got response: "+r.toString();
		string newtreehash := r.payload.getString("sha");
		builder.createRestAPIQuery("POST", "/git/commits", {"message":<any>"Update C8Y", "parents":[headcommithash], "tree":newtreehash, "author":{"name":"EPL Apps", "email":"automatic.eplapps@streaminganalytics.c8y"}}).execute(updateHEAD);
	}
	action updateHEAD(Response r)
	{
		log "Got response: "+r.toString();
		string newcommithash := r.payload.getString("sha");
		builder.createRestAPIQuery("PATCH", "/git/refs/heads/"+branch, {"sha":<any>newcommithash, "force":true}).execute(complete);
	}
	action complete(Response r)
	{
		log "Completed creating commit: "+r.toString();
		latestHash[0] := r.payload.getString("object.sha");
	}
	
	action checkForCommits()
	{
		builder.createRestAPIQuery("GET", "/git/ref/heads/"+branch, new any).execute(checkLatestHash);
	}
	action checkLatestHash(Response r)
	{
		log "Response="+r.toString();
		string remoteHash := r.payload.getString("object.sha");
		ifpresent latestHash[0] as h {
			log "latestHash="+h+", remoteHash="+remoteHash;
			if remoteHash != h {
				updateFromGit(remoteHash, r.payload.getString("object.url"));
			}
		} else {
			log "Current hash unset, checking";
			updateFromGit(remoteHash, r.payload.getString("object.url"));
		}
		latestHash[0] := remoteHash;
	}
	action updateFromGit(string headrefhash, string headrefurl)
	{
		builder.createRestAPIQuery("GET", headrefurl, new any).execute(getHeadCommitForUpdate);
	}
	action getHeadCommitForUpdate(Response r)
	{
		log "Got response: "+r.toString();
		headcommithash := r.payload.getString("sha");
		string headtreehash := r.payload.getString("tree.sha");
		string headtreeurl := r.payload.getString("tree.url");
		builder.createRestAPIQuery("GET", headtreeurl+"?recursive=1", new any).execute(updateFromTree);
	}
	action updateFromTree(Response r)
	{
		log "Got response: "+r.toString();
		Functional tree := Functional(r.payload.getAny("tree"));
		any _ := tree.map(AnyExtractor.create).filter(Fn.partial(pathMatches, "epl/.*.mon")).map(Fn.partial(updateEPLFileFromGit, [tree.get()]));
		_ := tree.map(AnyExtractor.create).filter(Fn.partial(pathMatches, "model/.*.json")).map(Fn.partial(updateABModelFromGit, [tree.get()]));
	}
	static action pathMatches(string regex, AnyExtractor data) returns boolean
	{
		return data.getString("path").matches(regex);
	}
	action updateEPLFileFromGit(any tree, AnyExtractor eplfile)
	{
		log "Updating EPLFile from "+eplfile.toString();
		string path := eplfile.getString("path");
		string bloburl := eplfile.getString("url");
		EPLFile file := EPLFile.createFromGit(self, eplfile.getString("path"), tree);
		file.updateFromGit(Fn.partial(file.updateFromC8Y, Fn.partial(file.checkForUpdate, file.uploadToC8Y).exec).exec);
	}
	action updateEPLFileFromResponse(Response r)
	{
		string content := r.payload.getString("content");
		log "Updating EPLFile from "+content;
		log "decodes to "+Base64.decode(content);
	}	
	action updateABModelFromResponse(Response r)
	{
		string content := r.payload.getString("content");
		log "Updating ABModel from "+content;
		log "decodes to "+Base64.decode(content);
	}
	action updateABModelFromGit(any tree, AnyExtractor model)
	{
		log "Updating AB Model from "+model.toString();
		string path := model.getString("path");
		string bloburl := model.getString("url");
		commit[bloburl] := path;
		builder.createRestAPIQuery("GET", bloburl, new any).execute(updateABModelFromResponse);
	}
}

event GitHandler
{
	static action create(string owner, string repo, string PAT, string branch) returns GitHandler
	{
		return <GitHandler> Fn.setFields(new GitHandler, {
			"repo":<any>("https://api.github.com/repos/"+owner+"/"+repo),
			"auth":"Bearer "+PAT,
			"branch":branch,
			"http":HttpTransport.getOrCreateWithConfigurations("api.github.com", 443, {HttpTransport.CONFIG_TLS: "true", HttpTransport.CONFIG_BASE_PATH: "/repos/"+owner+"/"+repo}),
			"latestHash":[new optional<string>]
			});
	}
	string repo;
	string auth;
	string branch;
	HttpTransport http;
	sequence<optional<string>> latestHash;

	action updateEPLFile(string id, string name, string contents, string description, string state)
	{
		GitUpdate update := GitUpdate.create(repo, auth, branch, http, latestHash);
		EPLFile eplFile := EPLFile.createFromC8Y(update, id, name, description, state, contents);
		eplFile.updateFromGit(Fn.partial(eplFile.checkForUpdate, eplFile.commitToGit).exec);
	}

	action updateABModel(string name, string model, string description, string state)
	{
		GitUpdate update := GitUpdate.create(repo, auth, branch, http, latestHash);
		any _ := Functional({
			"model/"+name+".json": model,
			"model/"+name+".state": state,
			"model/"+name+".txt": description
		})
			.map(update.writeFile)
			.map(update.getWriteResponse);
			//.waitForAllCompleted("com.softwareag.connectivity.httpclient.Response", "id", update.doCommit);
	}

	action startHandler()
	{
		monitor.subscribe(ManagedObject.SUBSCRIBE_CHANNEL);
		on all ManagedObject(type="apama_eplfile") as a {
			AnyExtractor ae := AnyExtractor(a.params);
			if a.name != "App update logger" {
				updateEPLFile(a.id, a.name, ae.getStringOr("apama_eplfile.contents", ""), ae.getStringOr("apama_eplfile.description", ""), ae.getStringOr("apama_eplfile.state", "")); 
			}
		}
		on all ManagedObject(type="c8y_PublicAnalyticsBuilderModel") as a {
			AnyExtractor ae := AnyExtractor(a.params);
			updateABModel(a.name, JSONPlugin.toJSON(a.params.getOr("builderModel", new dictionary<any, any>)), ae.getStringOr("description", ""), JSONPlugin.toJSON({"mode":ae.getStringOr("mode", ""), "modeProperties":ae.getStringOr("modeProperties", ""), "state":ae.getStringOr("state", "")}));
		}
		on all wait(10.0) {
			GitUpdate.create(repo, auth, branch, http, latestHash).checkForCommits();
		}
		GitUpdate.create(repo, auth, branch, http, latestHash).checkForCommits();
	}

}

monitor GitHandlerMonitor {
	action onload() {
		monitor.subscribe(FindTenantOptionsResponse.SUBSCRIBE_CHANNEL);
		monitor.subscribe(FindManagedObjectResponse.SUBSCRIBE_CHANNEL);
		on FindTenantOptionsResponse(reqId=(<FindTenantOptions>Fn.sendToChannel(FindTenantOptions.SEND_CHANNEL, Fn.setFields(new FindTenantOptions, {"reqId":Util.generateReqId(), "category":"github"}))).reqId) as options {
			TenantOption opt;
			string owner:="";
			string repository:="";
			string pat:="";
			string branch:="";
			for opt in options.options {
				if opt.key = "owner" { owner:=opt.value; }
				if opt.key = "repo" { repository:=opt.value; }
				if opt.key = "PAT" { pat:=opt.value; }
				if opt.key = "branch" { branch:=opt.value; }
			}
			if owner!="" and repository!="" and pat!="" and branch!="" {
				GitHandler.create(owner, repository, pat, branch).startHandler();
			} else if owner!="" or repository!="" or pat!="" or branch!="" {
				log "You must specify tenant options for github.owner, github.repo, github.branch and github.PAT" at ERROR;
			}
		}
	}
}
